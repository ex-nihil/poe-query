// ignore identity for now
WHITESPACE = _{ " " }
identity = { "."{1} }
dollar = _{ "$" }

identifier = { (ASCII_ALPHANUMERIC | "_")+ }
digits = { ASCII_DIGIT+ }

slice_from = { digits+ }
slice_to = { digits+ }

recurse = { ".." }
index_reverse = { &ANY ~ "[-" ~ digits+ ~ "]" }
index = { &ANY ~ "[" ~ digits+ ~ "]" }
slice = { &ANY ~ "[" ~ slice_from? ~ ":" ~ slice_to? ~ "]" }
iterator = { &ANY ~ "[]" }

string = { (!"\"" ~ ANY)+ }
number = _{ unsigned_number | signed_number }
unsigned_number = { "(unsigned)"? ~ digits+ }
signed_number = { (("(signed)" ~ minus) | ("(signed)" | minus)) ~ digits+ }
quoted_string = _{ "\"" ~ string ~ "\"" }
datatypes = _{ quoted_string | number }

// assignment
variable = ${ dollar ~ identifier }
assign_variable = { "as" ~ variable }

// arithmetic
plus  = { "+" }
minus = { "-" }

// functions
select = { "select(" ~ bool_expression ~ ")" }
transpose = { "transpose" }
reduce_init_value = { datatypes ~ ";" }
reduce = { "reduce" ~ (!assign_variable ~ filter_with_op)+ ~ assign_variable ~ "(" ~ reduce_init_value ~ filter_with_op* ~ ")" }

// comparisons
equal = { "==" }
not_equal = { "!=" }
less_than = { "<" }
greater_than = { ">" }
compare = { equal | not_equal | less_than | greater_than }
bool_expression = _{ filter_with_op+ ~ compare ~ filter_with_op+ }

field = { identifier }
key = { identifier ~ ":" }

named_query = { key ~ (!("," ~ &key) ~ filter_with_op)+ }
object_construct = { "{" ~ (named_query ~ operator?)+ ~ "}" }
array_construction = { "[" ~ filter_with_op* ~ "]" }

optional = { "?" }

query = { filter_with_op+ }

filter = _{ reduce | assign_variable | variable | transpose | select | iterator | recurse | identity | field | index_reverse | index | slice }
filter_with_op = _{ operator? ~ (datatypes | filter | array_construction | object_construct) }

pipe = { "|" }
comma = { "," }

operator = _{ pipe | comma }

program = _{ SOI ~ filter_with_op+ }
