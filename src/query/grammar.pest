// ignore identity for now
WHITESPACE = _{ " " | "\n" }
identity = { "."{1} }
dollar = _{ "$" }
minus = { "-" }

identifier = { !"." ~ (ASCII_ALPHANUMERIC | "_")+ }
digits = @{ ASCII_DIGIT+ }

slice_from = { digits+ }
slice_to = { digits+ }

index = { &ANY ~ "[" ~ number+ ~ "]" }
slice = { &ANY ~ "[" ~ slice_from? ~ ":" ~ slice_to? ~ "]" }
iterator = { &ANY ~ "[]" }

string = { (!"\"" ~ ANY)+ }
number = _{ unsigned_number | signed_number }
unsigned_number = { "(unsigned)"? ~ digits+ }
signed_number = { (("(signed)" ~ minus) | ("(signed)" | minus)) ~ digits+ }
quoted_string = _{ ("\"" ~ string ~ "\"") | ("'" ~ string ~ "'") }
datatypes = _{ quoted_string | number }

// assignment
variable = ${ dollar ~ identifier }
assign_variable = { "as" ~ variable }

// arithmetic
operation = { add | subtract | multiply | divide | power }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" }

// functions
select = { "select(" ~ bool_expression ~ ")" }
zip_to_obj = { "zip_to_obj" } // not part of JQ
transpose = { "transpose" }
reduce_init_value = { (datatypes | array_construction | object_construct) ~ ";" }
reduce = { "reduce" ~ (!assign_variable ~ query)+ ~ assign_variable ~ "(" ~ reduce_init_value ~ expr* ~ ")" }
map = { "map(" ~ expr+ ~ ")"}

bool_expression = _{ query+ ~ compare ~ query+ }
    compare = { equal | not_equal | less_than | greater_than }
        equal = { "==" }
        not_equal = { "!=" }
        less_than = { "<" }
        greater_than = { ">" }

field = { identifier }
key = {  (quoted_string | identifier | "(" ~ (datatypes | filter)+ ~ ")")+ ~ ":" }

named_query = { key ~ (!("," ~ &key) ~ query)+ }
object_construct = { "{" ~ (named_query ~ operator?)* ~ "}" }
array_construction = { "[" ~ query* ~ "]" }

optional = { "?" }

functions = _ { map | reduce | transpose | zip_to_obj }
filter = _{ functions | assign_variable | variable | select | iterator | field | index | slice | identity }
query = _{ operator? ~ (datatypes | filter | array_construction | object_construct) }

pipe = { "|" }
comma = { "," }

operator = _{ pipe | comma }


expr = { term ~ (operation ~ term)* }
term = _{ query | "(" ~ expr ~ ")" } // precedence

program = _{ SOI ~ expr+}